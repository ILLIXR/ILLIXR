# Report configuration

# helper functions

# based on https://github.com/jimbraun/XCDF/blob/master/cmake/CMakePadString.cmake
function(pad_string RESULT REQUESTED_LENGTH VALUE)
    string(LENGTH "${VALUE}" VALUE_LENGTH)
    math(EXPR REQUIRED_PAD "${REQUESTED_LENGTH} - ${VALUE_LENGTH}")
    set(PAD ${VALUE})
    if(REQUIRED_PAD GREATER 0)
        math(EXPR REQUIRED_MINUS_ONE "${REQUIRED_PAD} - 1")
        foreach(BLAH RANGE ${REQUIRED_PAD})
            set(PAD "${PAD} ")
        endforeach()
    endif()
    set(${RESULT} "${PAD}" PARENT_SCOPE)
endfunction()

set(ILLIXR_SUMMARY_PADDING 35 CACHE STRING "Padding of each report summary line")
mark_as_advanced(ILLIXR_SUMMARY_PADDING)

function(report_value value msg)
    pad_string(padded_value ${ILLIXR_SUMMARY_PADDING} "  ${value}")
    message(STATUS "${padded_value}: ${msg}")
endfunction()

function(print_enabled_disabled value msg)
    pad_string(padded_value ${ILLIXR_SUMMARY_PADDING} "  ${msg}")
    if(value)
        message(STATUS "${padded_value}: Enabled")
    else()
        message(STATUS "${padded_value}: Disabled")
    endif()
endfunction()

function(report_padded varname)
    pad_string(padded_value ${ILLIXR_SUMMARY_PADDING} " ${varname}")
    message(STATUS "${padded_value}: ${${varname}}")
endfunction()

string(TOUPPER "${CMAKE_BUILD_TYPE}" CMAKE_BUILD_TYPE_UPPER)
message(STATUS "-------------------------------------------------------------")
message(STATUS "------------------- Configuration Options -------------------")
report_value("CMAKE_CXX_COMPILER_ID type" "${CMAKE_CXX_COMPILER_ID}")
report_value("CMAKE_CXX_COMPILER_VERSION" "${CMAKE_CXX_COMPILER_VERSION}")
report_value("CMake version"    "${CMAKE_VERSION}")
report_value("CMake generator"  "${CMAKE_GENERATOR}")
report_value("CMake build tool" "${CMAKE_BUILD_TOOL}")
report_value("Build type" "${CMAKE_BUILD_TYPE}")
report_value("C compilation flags" "${CMAKE_C_FLAGS} ${CMAKE_C_FLAGS_${CMAKE_BUILD_TYPE_UPPER}}")
report_value("C++ compilation flags" "${CMAKE_CXX_FLAGS} ${CMAKE_CXX_FLAGS_${CMAKE_BUILD_TYPE_UPPER}}")
message(STATUS "External Libraries")
foreach(ITEM IN LISTS EXTERNAL_LIBRARIES)
    if(${ITEM}_EXTERNAL)
        report_value("${ITEM}" "Download and Install")
    else()
        report_value("${ITEM}" "Found ${${ITEM}_VERSION}")
    endif()
endforeach()
message(STATUS "Build Plugins")
if(BUILD_GROUP)
    report_value("Build components type" "${BUILD_GROUP}")
endif()
foreach(ITEM IN LISTS PLUGIN_LIST EXT_PLUGIN_LIST)
    string(TOUPPER "USE_${ITEM}" ITEM_UPPER)
    print_enabled_disabled("${${ITEM_UPPER}}" "${ITEM}")
endforeach()
