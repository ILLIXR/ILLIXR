# generate the profile yaml files from the master plugins.yaml
function(generate_yaml)
    if(NOT EXISTS "${PROJECT_SOURCE_DIR}/plugins/plugins.yaml")
        message(FATAL_ERROR "plugins/plugins.yaml file is missing")
    endif()

    file(TIMESTAMP "${PROJECT_SOURCE_DIR}/plugins/plugins.yaml" PROFILE_FILE_TIMESTAMP "%s" UTC)

    if(DEFINED CACHE{LAST_YAML_BUILD} AND DEFINED CACHE{PLUGIN_LIST} AND DEFINED CACHE{VISUALIZER_LIST})
        message("Using cached profile yaml files")
    else()
        set(LAST_YAML_BUILD "0" CACHE INTERNAL "")
    endif()
    if($CACHE{LAST_YAML_BUILD} LESS ${PROFILE_FILE_TIMESTAMP})
        message("Rebuilding profile yaml files")
        set(TEMP_MOD_PLUG "" CACHE INTERNAL "")
        set(PROFILE_NAMES "")
        file(STRINGS "${PROJECT_SOURCE_DIR}/plugins/plugins.yaml" YAML_LINES)
        set(IN_ENV_VARS NO)
        set(INDENT_LEVEL 0)
        set(STARTED_PROFILES NO)
        foreach(LINE_ITEM IN LISTS YAML_LINES)
            set(HAVE_VIS False)
            if(LINE_ITEM STREQUAL "")
                continue()
            elseif(LINE_ITEM MATCHES "^#")
                continue()
            endif()
            string(FIND "${LINE_ITEM}" "      " INDENT_COUNT)
            if (INDENT_COUNT EQUAL 0)
                set(INDENT_LEVEL 1)
            else()
                set(INDENT_LEVEL 0)
            endif()
            if(PROFILE_WRITING)
                string(FIND "${LINE_ITEM}" "  - " NEW_PROFILE)
                if(${NEW_PROFILE} EQUAL 0)
                    string(REGEX REPLACE "  - ([a-zA-Z][a-zA-Z0-9_]*):" "\\1" PROFILE_NAME ${LINE_ITEM})
                    if(STARTED_PROFILES)
                        file(APPEND ${OUTFILE} "visualizers: ${USE_VIS}\n")
                        file(APPEND ${OUTFILE} "env_vars:\n")
                        foreach(E_VAR IN LISTS ENV_VAR_NAMES)
                            set(EV_NAME "EV_${E_VAR}")
                            string(TOLOWER "${E_VAR}" E_VAR_LOWER)
                            file(APPEND ${OUTFILE} "  ${E_VAR_LOWER}: ${${EV_NAME}}\n")
                        endforeach()
                    else()
                        set(STARTED_PROFILES YES)
                    endif()
                    set(ENV_VAR_NAMES "ENABLE_OFFLOAD;ENABLE_ALIGNMENT;ENABLE_VERBOSE_ERRORS;ENABLE_PRE_SLEEP")
                    set(EV_ENABLE_OFFLOAD "false")
                    set(EV_ENABLE_ALIGNMENT "false")
                    set(EV_ENABLE_VERBOSE_ERRORS "false")
                    set(EV_ENABLE_PRE_SLEEP "false")
                    set(USE_VIS "${VIS_NAMES}")

                    if(TEMP_MOD_PLUG)
                        set(TEMP_MOD_PLUG "${TEMP_MOD_PLUG}},\n")
                    endif()
                    list(APPEND PROFILE_NAMES ${PROFILE_NAME})
                    set(TEMP_MOD_PLUG "${TEMP_MOD_PLUG}    {\n      \"name\": \"${PROFILE_NAME}\"")
                    set(OUTFILE "${PROJECT_SOURCE_DIR}/profiles/${PROFILE_NAME}.yaml")
                    file(WRITE ${OUTFILE} "# This file was auto generated, take caution if manually editing.\n")
                    continue()
                endif()
            endif()
            string(REPLACE " " "" ITEM ${LINE_ITEM})
            string(REGEX REPLACE "^([^:]*):(.*)" "\\1;\\2" ITEM_LIST "${ITEM}")
            list(GET ITEM_LIST 0 ENTRY)
            #set(ALL_FILE "${PROJECT_SOURCE_DIR}/profiles/all.yaml")
            if (IN_ENV_VARS)
                if (INDENT_LEVEL EQUAL 0)
                    set(IN_ENV_VARS NO)
                else()
                    list(GET ITEM_LIST 1 EV_VALUE)
                    string(TOUPPER ${ENTRY} ENTRY_UPPER)
                    list(FIND ENV_VAR_NAMES ${ENTRY_UPPER} VAR_FOUND)
                    if (${VAR_FOUND} LESS 0)
                        list(APPEND ENV_VAR_NAMES ${ENTRY_UPPER})
                    endif()
                    set(TEMP_NAME "EV_${ENTRY_UPPER}")
                    set(${TEMP_NAME} ${EV_VALUE})
                endif()
            endif()
            if (NOT IN_ENV_VARS AND INDENT_LEVEL EQUAL 0)
                if(ENTRY STREQUAL "all_plugins")
                    list(GET ITEM_LIST 1 PLUGIN_NAMES)
                    string(REPLACE "," ";" TEMP_PLUGIN_LIST "${PLUGIN_NAMES}")
                    set(PLUGIN_LIST ${TEMP_PLUGIN_LIST} CACHE INTERNAL "" FORCE)
                elseif(ENTRY STREQUAL "all_visualizers")
                    list(GET ITEM_LIST 1 VIS_NAMES)
                    string(REPLACE "," ";" TEMP_VIS_LIST "${VIS_NAMES}")
                    set(VISUALIZER_LIST ${TEMP_VIS_LIST} CACHE INTERNAL "" FORCE)
                    set(VIS_NAMES "${VIS_NAMES}" CACHE INTERNAL "" FORCE)
                elseif(ENTRY STREQUAL "visualizers")
                    set(USE_VIS "")
                    if(NOT PROFILE_WRITING)
                        message(FATAL_ERROR "plugins/plugins.yaml has incorrect format")
                    endif()
                    list(GET ITEM_LIST 1 VISUALIZER)
                    string(REPLACE "," ";" TEMP_VIS "${VISUALIZER}")
                    foreach(V_ITEM IN LISTS TEMP_VIS)
                        set(VIS_FOUND No)
                        foreach(ITEM IN LISTS VISUALIZER_LIST)
                            if(V_ITEM STREQUAL ${ITEM})
                                list(APPEND USE_VIS ${V_ITEM})
                                set(VIS_FOUND Yes)
                            endif()
                        endforeach()
                        if(NOT VIS_FOUND)
                            message(FATAL_ERROR "Improper visualizer given, must be one of ${VISUALIZER_LIST}. ")
                        endif()
                    endforeach()
                elseif(ENTRY STREQUAL "use_monado")
                    if(NOT PROFILE_WRITING)
                        message(FATAL_ERROR "plugins/plugins.yaml has incorrect format")
                    endif()
                    list(GET ITEM_LIST 1 MONADO_STR)
                    file(APPEND ${OUTFILE} "use_monado: ${MONADO_STR}\n")
                elseif(ENTRY STREQUAL "build_flags")
                    if(NOT PROFILE_WRITING)
                        message(FATAL_ERROR "plugins/plugins.yaml has incorrect format")
                    endif()
                    list(GET ITEM_LIST 1 LOCAL_BUILD_FLAGS)
                    file(APPEND ${OUTFILE} "build_flags: ${LOCAL_BUILD_FLAGS}\n")
                    set(TEMP_MOD_PLUG "${TEMP_MOD_PLUG},\n    \"build_flags\": \"${LOCAL_BUILD_FLAGS}\"")
                elseif(ENTRY STREQUAL "profiles")
                    set(PROFILE_WRITING TRUE)
                elseif(KEY STREQUAL "install_prefix")
                    if(NOT PROFILE_WRITING)
                        message(FATAL_ERROR "plugins/plugins.yaml has incorrect format")
                    endif()
                    list(GET ITEM_LIST 1 PREFIX)
                    file(APPEND ${OUTFILE} "install_prefix: ${PREFIX}\n")
                elseif(ENTRY STREQUAL "plugins")
                    list(GET ITEM_LIST 1 PLUGIN_NAMES)
                    file(APPEND ${OUTFILE} "plugins: ${PLUGIN_NAMES}\n")
                    string(REPLACE "," "\", \"" MOD_PLUGS "${PLUGIN_NAMES}")
                    set(TEMP_MOD_PLUG "${TEMP_MOD_PLUG},\n      \"plugins\": [\"${MOD_PLUGS}\"]")
                elseif(ENTRY STREQUAL "run")  #?
                    list(GET ITEM_LIST 1 PLUGIN_NAMES)
                    file(APPEND ${OUTFILE} "run: ${PLUGIN_NAMES}\n")
                elseif (ENTRY STREQUAL "build_type")
                    list(GET ITEM_LIST 1 BLD_TYPE)
                    file(APPEND ${OUTFILE} "build_type: ${BLD_TYPE}\n")
                elseif(ENTRY STREQUAL "env_vars")
                    set(IN_ENV_VARS YES)
                else()
                    message(WARNING "Unknown keyword: ${ENTRY}")
                endif(ENTRY STREQUAL "all_plugins")
            endif(NOT IN_ENV_VARS AND INDENT_LEVEL EQUAL 0)
        endforeach ()
        file(APPEND ${OUTFILE} "visualizers: ${USE_VIS}\n")
        file(APPEND ${OUTFILE} "env_vars:\n")
        foreach(E_VAR IN LISTS ENV_VAR_NAMES)
            set(EV_NAME "EV_${E_VAR}")
            string(TOLOWER "${E_VAR}" E_VAR_LOWER)
            file(APPEND ${OUTFILE} "  ${E_VAR_LOWER}: ${${EV_NAME}}\n")
        endforeach()
        set(TEMP_MOD_PLUG "${TEMP_MOD_PLUG}\n    }\n")
        string(REPLACE "\n" "||" MODULE_PLUGINS "${TEMP_MOD_PLUG}")
        set(MODULE_PLUGINS "${MODULE_PLUGINS}" CACHE INTERNAL "")
        string(TIMESTAMP CURRENT_TIME "%s" UTC)
        set(LAST_YAML_BUILD ${CURRENT_TIME} CACHE INTERNAL "")
        set(ILLIXR_PROFILE_NAMES ${PROFILE_NAMES} CACHE INTERNAL "")
    endif()
endfunction()

# read the provided yaml file for arguments/options
function(read_yaml FILENAME)
    # check that the file exists
    if(NOT EXISTS "${FILENAME}")
        FIND_FILE(FILEPATH ${FILENAME}
                PATHS profiles ${CMAKE_SOURCE_DIR} $ENV{HOME} $ENV{HOME}/.illixr
                PATH_SUFFIXES profiles
                NO_CACHE
                )
        if(${FILEPATH} STREQUAL "FILEPATH-NOTFOUND")
            message(FATAL_ERROR "Could not find ${FILENAME} in any of the following paths:\n    profiles\n    ${CMAKE_SOURCE_DIR}\n    ${CMAKE_SOURCE_DIR}/profiles\n    $ENV{HOME}\n    $ENV{HOME}/profiles\n    $ENV{HOME}/.illixr\n    $ENV{HOME}/.illixr/profiles")
        endif()
        set(FILENAME ${FILEPATH})
        message(STATUS "Reading profile file ${FILENAME}")
    endif()

    set(EV_ENABLE_OFFLOAD "false" PARENT_SCOPE)
    set(EV_ENABLE_ALIGNMENT "false" PARENT_SCOPE)
    set(EV_ENABLE_VERBOSE_ERRORS "false" PARENT_SCOPE)
    set(EV_ENABLE_PRE_SLEEP "false" PARENT_SCOPE)
    file(STRINGS ${FILENAME} YAML_LINES)
    set(VIS_FOUND No)
    set(IN_ENV_VARS NO)
    set(ENV_VAR_NAMES "ENABLE_OFFLOAD;ENABLE_ALIGNMENT;ENABLE_VERBOSE_ERRORS;ENABLE_PRE_SLEEP")
    set(INDENT_LEVEL 0)
    foreach(LINE IN LISTS YAML_LINES)
        string(FIND ${LINE} "  " INDENT_COUNT)
        if (INDENT_COUNT EQUAL 0)
            set(INDENT_LEVEL 1)
        else()
            set(INDENT_LEVEL 0)
        endif()
        string(REPLACE " " "" ITEM ${LINE})
        string(REGEX REPLACE "^([^:]*):(.*)" "\\1;\\2" ITEM_LIST "${ITEM}")
        list(GET ITEM_LIST 0 KEY)
        if (IN_ENV_VARS)
            if (INDENT_LEVEL EQUAL 0)
                set(IN_ENV_VARS NO)
            else()
                list(GET ITEM_LIST 1 EV_VALUE)
                string(TOUPPER "${KEY}" KEY_UPPER)
                list(FIND ENV_VAR_NAMES ${KEY_UPPER} VAR_FOUND)
                if (${VAR_FOUND} LESS 0)
                    list(APPEND ENV_VAR_NAMES ${KEY_UPPER})
                endif()
                set(TEMP_NAME "EV_${KEY_UPPER}")
                set(${TEMP_NAME} ${EV_VALUE} PARENT_SCOPE)
            endif()
        endif()
        if (NOT IN_ENV_VARS AND INDENT_LEVEL EQUAL 0)
            if(KEY STREQUAL "plugins")
                list(GET ITEM_LIST 1 PLUGIN_NAMES)
                set(PLUGIN_UNORDERED "${PLUGIN_NAMES}" PARENT_SCOPE)
                string(REPLACE "," ";" TEMP_PLUGIN_LIST "${PLUGIN_NAMES}")
                foreach(PL IN LISTS TEMP_PLUGIN_LIST)
                    string(TOUPPER "USE_${PL}" PL_UPPER)
                    set(${PL_UPPER} ON PARENT_SCOPE)
                endforeach()
            elseif(KEY STREQUAL "visualizers")
                list(GET ITEM_LIST 1 VIS_NAMES)
                string(REPLACE "," ";" TEMP_VIS_LIST "${VIS_NAMES}")
                list(LENGTH TEMP_VIS_LIST VIS_LEN)
                if(VIS_LEN GREATER 0 AND (NOT VISUALIZER OR VISUALIZER_TO_USE STREQUAL "ALL"))
                    foreach(VI IN LISTS TEMP_VIS_LIST)
                        string(TOUPPER "USE_${VI}" VI_UPPER)
                        set(${VI_UPPER} ON PARENT_SCOPE)
                    endforeach ()
                    set(VISUALIZER_TO_USE ${TEMP_VIS_LIST} CACHE INTERNAL "")
                elseif(NOT VISUALIZER_TO_USE STREQUAL "ALL")
                    foreach(VI IN LISTS VISUALIZER_TO_USE)
                        string(TOUPPER "USE_${VI}" VI_UPPER)
                        set(${VI_UPPER} ON PARENT_SCOPE)
                    endforeach()
                endif()
                set(VIS_FOUND)
            elseif(KEY STREQUAL "use_monado")
                list(GET ITEM_LIST 1 MONADO_STR)
                string(TOUPPER ${MONADO_STR} MONADO_UPPER)
                if(MONADO_UPPER STREQUAL "TRUE")
                    set(USE_MONADO ON PARENT_SCOPE)
                endif()
            elseif(KEY STREQUAL "build_flags")
                list(GET ITEM_LIST 1 TEMP_BUILD_FLAGS)
                string(REPLACE "," " " LOC_BUILD_FLAGS "${TEMP_BUILD_FLAGS}")
                set(LOCAL_BUILD_FLAGS ${LOC_BUILD_FLAGS} PARENT_SCOPE)
            elseif(KEY STREQUAL "data")
                list(SUBLIST ITEM_LIST 1 -1 TDATA_LIST)
                list(JOIN TDATA_LIST ":" DATA_FILE_ITEM)
                set(DATA_FILE ${DATA_FILE_ITEM} PARENT_SCOPE)
            elseif (KEY STREQUAL "build_type")
                if(CMAKE_BUILD_TYPE STREQUAL "")
                    list(GET ITEM_LIST 1 BLD_TYPE)
                    set(CMAKE_BUILD_TYPE ${BLD_TYPE} PARENT_SCOPE)
                endif()
            elseif(KEY STREQUAL "install_prefix")
                if(CMAKE_INSTALL_PREFIX_INITIALIZED_TO_DEFAULT)
                    list(GET ITEM_LIST 1 PREFIX)
                    set(CMAKE_INSTALL_PREFIX ${PREFIX} PARENT_SCOPE)
                endif()
            elseif(KEY STREQUAL "enable_offload")
                list(GET ITEM_LIST 1 VAL)
                message(DEPRECATION "This yaml file is using an older format, which will be deprecated in a future release. See the documentation on the new format.")
                set(EV_ENABLE_OFFLOAD ${VAL} PARENT_SCOPE)
            elseif(KEY STREQUAL "enable_alignment")
                list(GET ITEM_LIST 1 VAL)
                message(DEPRECATION "This yaml file is using an older format, which will be deprecated in a future release. See the documentation on the new format.")
                set(EV_ENABLE_ALIGNMENT ${VAL} PARENT_SCOPE)
            elseif(KEY STREQUAL "enable_verbose_errors")
                list(GET ITEM_LIST 1 VAL)
                message(DEPRECATION "This yaml file is using an older format, which will be deprecated in a future release. See the documentation on the new format.")
                set(EV_ENABLE_VERBOSE_ERRORS ${VAL} PARENT_SCOPE)
            elseif(KEY STREQUAL "enable_pre_sleep")
                list(GET ITEM_LIST 1 VAL)
                message(DEPRECATION "This yaml file is using an older format, which will be deprecated in a future release. See the documentation on the new format.")
                set(EV_ENABLE_PRE_SLEEP ${VAL} PARENT_SCOPE)
            elseif(KEY STREQUAL "env_vars")
                set(IN_ENV_VARS YES)
            endif()
        endif()
    endforeach()
    set(ENVIRONMENT_VARS ${ENV_VAR_NAMES} PARENT_SCOPE)
    if(NOT VIS_FOUND)
        if(VISUALIZER_TO_USE STREQUAL "ALL")
            set(VISUALIZER_TO_USE ${VISUALIZER_LIST} CACHE INTERNAL "")
        endif()
        foreach(VI IN LISTS VISUALIZER_TO_USE)
            string(TOUPPER "USE_${VI}" VI_UPPER)
            set(${VI_UPPER} ON PARENT_SCOPE)
        endforeach()
    endif()
endfunction()

# function to make sure all of the plugins actually exist
function(check_plugins)
    foreach(ITEM IN LISTS PLUGIN_LIST)
        string(TOLOWER ${ITEM} PLUGIN)
        string(REPLACE "." "/" PLUGIN "${PLUGIN}")
        if(NOT EXISTS "${CMAKE_SOURCE_DIR}/plugins/${PLUGIN}")
            message(FATAL_ERROR "Plugin directory for ${PLUGIN} does not exist in ${CMAKE_SOURCE_DIR}/plugins")
        endif()
    endforeach()

    foreach(ITEM IN LISTS VISUALIZER_LIST)
        string(TOLOWER ${ITEM} PLUGIN)
        if(NOT EXISTS "${CMAKE_SOURCE_DIR}/plugins/${PLUGIN}")
            message(FATAL_ERROR "Visualizer directory for ${PLUGIN} does not exist in ${CMAKE_SOURCE_DIR}/plugins")
        endif()
    endforeach()
endfunction()

# check for 3rd party dependencies that are not parts of OS repos
macro(get_external proj)
    if(NOT ${proj}_EXTERNAL)
        list(APPEND EXTERNAL_LIBRARIES "${proj}")
        set(${proj}_EXTERNAL No)
        set(${proj}_DEP_STR "")
        include(${CMAKE_SOURCE_DIR}/cmake/Get${proj}.cmake)
    endif()
endmacro()

# check for 3rd party dependencies that are not parts of OS repos, but from included plugins
macro(get_external_for_plugin proj)
    if(NOT ${proj}_EXTERNAL)
        list(APPEND EXTERNAL_LIBRARIES "${proj}")
        set(${proj}_EXTERNAL No PARENT_SCOPE)
        set(${proj}_DEP_STR "" PARENT_SCOPE)
        include(${CMAKE_SOURCE_DIR}/cmake/Get${proj}.cmake)
        set(EXTERNAL_LIBRARIES ${EXTERNAL_LIBRARIES} PARENT_SCOPE)
        set(${proj}_EXTERNAL ${${proj}_EXTERNAL} PARENT_SCOPE)
        set(${proj}_DEP_STR ${${proj}_DEP_STR} PARENT_SCOPE)
    endif()
endmacro()

macro(install_shaders SHADER_LIST PLUG LIB_NAME)
    foreach(ITEM IN LISTS ${SHADER_LIST})
        if(DO_DEBUG)
            set(LOC "Debug")
        else()
            set(LOC "Release")
        endif()
        set(INST_LOC "${CMAKE_INSTALL_PREFIX}/share/illixr/shaders/${PLUG}/${LOC}")
        install(FILES "${ITEM}"
                DESTINATION ${INST_LOC})
    endforeach()
    target_compile_definitions(${LIB_NAME} PUBLIC -DSHADER_FOLDER=\"${INST_LOC}\")
endmacro()
